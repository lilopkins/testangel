// This rule is for testing in the pest.rs editor only. It is not used in code.
Multiple = {
  SOI ~
  (Descriptor ~ "\n"+)* ~
  EOI
}

Line = {
  SOI ~
  Descriptor ~
  EOI
}

Descriptor = {
  "--:" ~
  (TypedDescriptor | KeyValueDescriptor | FlagDescriptor)
}

TypedDescriptor = {
  TypedDescriptorKind ~ Type ~ Value
}

Type = @{
  "Integer" | "Decimal" | "Boolean" | "Text"
}

KeyValueDescriptor = {
  KeyValueDescriptorKind ~ Value
}

FlagDescriptor = {
  FlagDescriptorKind
}

TypedDescriptorKind = @{
  "param" | "return"
}

KeyValueDescriptorKind = @{
  "name" | "group" | "creator" | "description"
}

FlagDescriptorKind = @{
  "hide-in-flow-editor"
}

Value = {
  (ASCII_ALPHANUMERIC | PUNCTUATION | SYMBOL | " ")+
}

WHITESPACE = _{ " " }
